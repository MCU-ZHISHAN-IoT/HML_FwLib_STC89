# ------------------------------------------------------------------------
# Author     : Weilun Fong | wlf@zhishan-iot.tk
# Date       : 2022-01-16
# Description: project Makefile
# E-mail     : mcu@zhishan-iot.tk
# Make-tool  : CMake
# Page       : https://hw.zhishan-iot.tk/page/hml/detail/fwlib_stc89.html
# Project    : HML_FwLib_STC89
# Version    : v0.0.1
# ------------------------------------------------------------------------

# CMake require
cmake_minimum_required(VERSION 3.17 FATAL_ERROR)

# Config toolchain
set(CMAKE_TOOLCHAIN_FILE cmake/toolchain.cmake)
set(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)

# Project define
project(
    HML_FwLib_STC89
    DESCRIPTION "A lite firmware library for STC micro STC89 series MCU based on SDCC complier"
    LANGUAGES C)

# Path define
set(BUILDDIR ${CMAKE_SOURCE_DIR}/build)
set(CMAKEDIR ${CMAKE_SOURCE_DIR}/cmake)

# File define
set(LIB libhml_stc89)
set(EXECUTABLE_NAME output)
aux_source_directory(${CMAKE_SOURCE_DIR}/src SOURCES)

# ----------------------------------------
#  Compile rule
# ----------------------------------------
# Load config
include(${CMAKEDIR}/config.cmake)
include(${CMAKEDIR}/version.cmake)
include(${CMAKEDIR}/cflags.cmake)

# Build library
include_directories(${CMAKE_SOURCE_DIR}/inc)
add_library(${LIB} STATIC ${SOURCES})

# Build executable
if(DEFINED EXECUTABLE_C)

    add_executable(${EXECUTABLE_NAME} ${EXECUTABLE_C})
    target_link_libraries(${EXECUTABLE_NAME} ${LIB})

    # generate a bin file after build
    add_custom_command(TARGET ${EXECUTABLE_NAME} POST_BUILD
        COMMENT
            "Generate .hex file finally"
        DEPENDS
            ${EXECUTABLE_NAME}.ihx
        COMMAND
            ${PACKIHX} ${EXECUTABLE_NAME}.ihx > ${EXECUTABLE_NAME}.hex
    )

    # custom clean
    SET(additional_clean_files ${EXECUTABLE_NAME}.lk ${EXECUTABLE_NAME}.map ${EXECUTABLE_NAME}.mem ${EXECUTABLE_NAME}.hex)
    set_target_properties(
        ${EXECUTABLE_NAME}
        PROPERTIES
            ADDITIONAL_CLEAN_FILES "${additional_clean_files}"
    )

endif()

# ----------------------------------------
#  Custom targets
# ----------------------------------------
# target <distclean>: clean all temporay and output files
add_custom_target(distclean
    COMMENT
        "Clean all temporay files"
    COMMAND
        rm -rf ${BUILDDIR}/*
)
# target <library>: build library file
add_custom_target(library
    DEPENDS
        ${LIB}
)
# target <rebuild>: clean all temporay and output files
add_custom_target(rebuild
    COMMENT
        "Rebuild project"
    COMMAND
        ${CMAKE_COMMAND} --build . -- clean && rm -f ${EXECUTABLE_NAME}*
    COMMAND
        nproc | xargs ${CMAKE_COMMAND} --build . --parallel
    VERBATIM
)
# target <version>: show version information
add_custom_target(version
    COMMAND
        echo -e "\
\\n\
General purpose CMake based Makefile for HML firmware library. \\n\
Copyright(c) 2021-2022 ZHISHAN-IoT, all rights reserved. \\n\
\\n\
Adapted for ${PROJECT_NAME} v${PROJECT_VERSION} by Jiabin Hsu <zsiothsu@zhishan-iot.tk> \\n\
Report bugs and patches to <wlf@zhishan-iot.tk>\\n"
    VERBATIM
)
# target <usage>: show usage
add_custom_target(usage
    COMMAND
        echo -e "\
\\n\
Build: \\n\
    all        - Build target, include HML library file and user code. \\n\
    library    - Only build HML library file. \\n\
    rebuild    - Rebuild project. \\n\
\\n\
Clean: \\n\
    clean      - Delete all temporary and output files.\\n\
    distclean  - Delete all files generated by CMake. \\n\
\\n\
Info: \\n\
    help       - CMake builtin target, list all executable targets. \\n\
    usage      - Show usage. \\n\
    version    - Show version information. \\n\
\\n\
Report bugs and patches to <wlf@zhishan-iot.tk>\\n"
    VERBATIM
)

# ----------------------------------------
#  Show build information
# ----------------------------------------
include(${CMAKEDIR}/info.cmake)
